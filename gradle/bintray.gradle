def siteUrl = 'https://github.com/subroh0508/kotlin-material-ui'
def githubUrl = 'https://github.com/subroh0508/kotlin-material-ui'

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "subroh0508"
            name "Subroh Nishikori"
            email "subroh.0508@gmail.com"
        }
    }

    scm {
        url githubUrl
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    publications = ['ToMavenPublication']

    publish = false
    pkg {
        repo = 'maven'
        name = 'Kotlin-Material-UI'
        licenses = ['MIT']
        websiteUrl = siteUrl
        vcsUrl = githubUrl + '.git'
        issueTrackerUrl = githubUrl + '/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

publishing {
    publications {
        ToMavenPublication(MavenPublication) {
            from components.kotlin
            artifact kotlinSourcesJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Kotlin Wrapper Library of Material-UI')
                root.appendNode('name', 'Kotlin-Material-UI')
                root.appendNode('url', githubUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

tasks.JsJar {
    from "${rootDir.path}/build/js/packages/kotlin-material-ui-core/package.json"
}
